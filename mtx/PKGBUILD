# Maintainer: Christian Autermann <autermann@gmail.com>

pkgname=mtx
pkgver=1.3.12
pkgrel=1
pkgdesc="Tools used to control media changer devices such as Tape and DVD/CD libraries"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/mtx/"
license=('GPL')
depends=('glibc')
provides=()
options=('strip')
source=(http://sourceforge.net/projects/${pkgname}/files/${pkgname}-stable/${pkgver}/${pkgname}-${pkgver}.tar.gz
        destdir.patch)
sha256sums=('0261c5e90b98b6138cd23dadecbc7bc6e2830235145ed2740290e1f35672d843'
            'b417b0d0435d07e3834bb66b46306c398fa573048c63ea8f53323ad113edf419')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np0 < ../destdir.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
